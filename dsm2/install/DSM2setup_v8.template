[Setup]

AppName=DSM2_v8
AppVerName=version @{Version_Main}@{Version_Status}.@{Version_Distribute}
OutputBaseFilename=DSM2setup_@{Version_Main}@{Version_Status}.@{Version_Distribute}
AppPublisher=CA DWR
AppPublisherURL=http://baydeltaoffice.water.ca.gov/modeling/deltamodeling/models/dsm2/dsm2.cfm
AppSupportURL=http://baydeltaoffice.water.ca.gov/modeling/deltamodeling/models/dsm2/dsm2.cfm
AppUpdatesURL=http://baydeltaoffice.water.ca.gov/modeling/deltamodeling/models/dsm2/dsm2.cfm
DefaultDirName=d:\delta\dsm2_v8
DefaultGroupName=DSM2_v8
AllowNoIcons=yes
;Compression=lzma/ultra
Compression=lzma/fast
CompressionThreads=auto
SolidCompression=no
ChangesEnvironment=yes
UninstallDisplayName=DSM2_v8
UninstallFilesDir={app}\bin\uninstall
;UninstallLogMode=new
UninstallLogMode=append
InfoBeforeFile=".\infoFile.rtf"
OutputDir="."
AlwaysRestart = no
PrivilegesRequired = admin
LicenseFile="..\license\COPYRIGHT.txt"


[Languages]

Name: "english"; MessagesFile: "compiler:Default.isl"


[Tasks]

Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"
Name: "quicklaunchicon"; Description: "{cm:CreateQuickLaunchIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked


[Dirs]

Name: "{app}\common_input"
Name: "{app}\timeseries"
Name: "{app}\vista"
Name: "{app}\scripts"
Name: "{app}\bin"
Name: "{app}\extras"
Name: "{app}\tutorials"


[Components]

Name: "main"; Description: "Main Files"; Types: full compact custom;


[Files]

Source: "..\doc\*";                      DestDir: "{app}\documentation\";          Flags: ignoreversion recursesubdirs createallsubdirs ; Components: main
Source: "..\bin\*";                      DestDir: "{app}\bin\";                    Flags: ignoreversion recursesubdirs createallsubdirs ; Components: main
Source: "..\scripts\*";                  DestDir: "{app}\scripts\";                Flags: ignoreversion recursesubdirs createallsubdirs ; Components: main
Source: "..\common_input\*";             DestDir: "{app}\common_input\";           Flags: ignoreversion recursesubdirs createallsubdirs ; Components: main

Source: "..\tutorials\pdf\*";            Destdir: "{app}\tutorials\";              Flags: ignoreversion recursesubdirs createallsubdirs ; Components: main
Source: "..\tutorials\data\*";           Destdir: "{app}\tutorials\data\";         Flags: ignoreversion recursesubdirs createallsubdirs ; Components: main
Source: "..\tutorials\simple\*";         Destdir: "{app}\tutorials\simple\";       Flags: ignoreversion recursesubdirs createallsubdirs ; Components: main
Source: "..\tutorials\historical\*";     Destdir: "{app}\tutorials\historical\";   Flags: ignoreversion recursesubdirs createallsubdirs ; Components: main

Source: "..\license\*";                  Destdir: "{app}\";                        Flags: ignoreversion recursesubdirs createallsubdirs ; Components: main

Source: "..\studies\*";                  Destdir: "{app}\studies\";                Flags: ignoreversion recursesubdirs createallsubdirs ; Components: main
Source: "..\study_templates\*";          Destdir: "{app}\study_templates\";        Flags: ignoreversion recursesubdirs createallsubdirs ; Components: main
Source: "..\timeseries\*";               DestDir: "{app}\timeseries\";             Flags: ignoreversion recursesubdirs createallsubdirs ; Components: main
Source: "..\vista\*";                    DestDir: "{app}\vista\";                  Flags: ignoreversion recursesubdirs createallsubdirs ; Components: main
Source: "..\extras\*";                   DestDir: "{app}\extras\";                 Flags: ignoreversion recursesubdirs createallsubdirs ; Components: main


[Icons]

Name: "{app}\DSM2_documentation";   Filename: "{app}\documentation\html\toc.html"
Name: "{group}\DSM2_documentation"; Filename: "{app}\documentation\html\toc.html"
Name: "{group}\DSM2_grid";          Filename: "{app}\documentation\DSM2_Grid2.0.pdf"
Name: "{group}\DSM2_v8";            Filename: "{app}\"
Name: "{group}\Uninstall";          Filename: "{uninstallexe}"
Name: "{userdesktop}\DSM2_v8";      Filename: "{app}\"; IconFilename: "{app}\bin\DSM2_v8.ico"; Tasks: desktopicon

[Registry]

Root: HKLM; Subkey: "SYSTEM\CurrentControlSet\Control\Session Manager\Environment"; ValueType: string; ValueName: "DSM2_HOME";   ValueData:  "{app}";         Flags: uninsdeletevalue;
Root: HKLM; Subkey: "SYSTEM\CurrentControlSet\Control\Session Manager\Environment"; ValueType: string; ValueName: "VISTA_HOME";  ValueData:  "{app}\vista";   Flags: uninsdeletevalue;

Root: HKLM; Subkey: "SOFTWARE\DSM2\v8";      ValueType: dword;   ValueName: "Version_Distribute";  ValueData: @{Version_Distribute};  Flags: uninsdeletevalue;
Root: HKLM; Subkey: "SOFTWARE\DSM2\v8";      ValueType: dword;   ValueName: "Version_SourceCode";  ValueData: @{Version_SourceCode};  Flags: uninsdeletevalue;
Root: HKLM; Subkey: "SOFTWARE\DSM2\v8";      ValueType: string;  ValueName: "Version_Status";      ValueData: "@{Version_Status}";    Flags: uninsdeletevalue;

Root: HKLM; Subkey: "SOFTWARE\DSM2\v8\path"; ValueType: string; ValueName: "Vista";             ValueData: "%DSM2_HOME%\vista\bin;";   Flags: uninsdeletevalue;
Root: HKLM; Subkey: "SOFTWARE\DSM2\v8\path"; ValueType: string; ValueName: "DSM2";              ValueData: "%DSM2_HOME%\bin;";         Flags: uninsdeletevalue;
Root: HKLM; Subkey: "SOFTWARE\DSM2\v8\path"; ValueType: string; ValueName: "Uninstallexepath";  ValueData: "{uninstallexe}";           Flags: uninsdeletevalue;

[Run]

Filename: "{app}\documentation\html\toc.html"; Description: "View DSM2 documentation"; Flags: postinstall shellexec


[Code]

function UninstallerNotCalled(Uninstallexepath1: string; Uninstallexepath2: string): Boolean;

var
  ResultCode: Integer;
  
begin

    if MsgBox('Previous DSM2_v8 version will be uninstalled before continue.', mbConfirmation, MB_YESNO or MB_DEFBUTTON1) = IDYES then
         begin
              if FileExists(Uninstallexepath1) then
                  begin
                       Exec(Uninstallexepath1, '/SILENT', '', SW_SHOW, ewWaitUntilTerminated, ResultCode);
                       Sleep(600);
                       Result := False;
                  end;
              if (FileExists(Uninstallexepath2) and (Uninstallexepath2 <> Uninstallexepath1) ) then
                  begin
                       Exec(Uninstallexepath2, '/SILENT', '', SW_SHOW, ewWaitUntilTerminated, ResultCode);
                       Sleep(200);
                       Result := False;  /// Continue installation
                  end;
         end
    else
         begin
              MsgBox('Current installation will be aborted.', mbInformation, MB_OK);
              Result := True;   /// True to abort installation
         end;

end;


procedure CurUninstallStepChanged(CurUninstallStep: TUninstallStep);

var
  OldPath: String;

begin

  RegQueryStringValue(HKEY_LOCAL_MACHINE, 'SYSTEM\CurrentControlSet\Control\Session Manager\Environment', 'Path',       OldPath);

  case CurUninstallStep of
    usUninstall:
      begin

        /// clean double ;
        StringChangeEx(OldPath,       ';;',                 ';', True);
        
        /// clean path
        StringChangeEx(OldPath,        '%DSM2_HOME%\bin;',         '', True);
        StringChangeEx(OldPath,       ';%DSM2_HOME%\bin',          '', True);
        StringChangeEx(OldPath,        '%DSM2_HOME%\vista\bin;',   '', True);
        StringChangeEx(OldPath,       ';%DSM2_HOME%\vista\bin',    '', True);

        RegWriteExpandStringValue(HKEY_LOCAL_MACHINE, 'SYSTEM\CurrentControlSet\Control\Session Manager\Environment', 'Path',       OldPath);

      end;
  end;
end;
    
    
procedure CurStepChanged(CurStep: TSetupStep);

var
  Uninstallexepath1: String;
  Uninstallexepath2: String;
  OldPath: String;
  OldPythonPath: String;

begin

case CurStep of
    ssInstall:

begin

    /// check if previous v8 exist
    if   (RegQueryStringValue(HKEY_LOCAL_MACHINE, 'SOFTWARE\DSM2\v8\path', 'Uninstallexepath', Uninstallexepath1)
       or RegQueryStringValue(HKEY_LOCAL_MACHINE, 'SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\DSM2_v8_is1','UninstallString', Uninstallexepath2) )

    then
       begin
          RegQueryStringValue(HKEY_LOCAL_MACHINE, 'SOFTWARE\DSM2\v8\path', 'Uninstallexepath', Uninstallexepath1);
          RegQueryStringValue(HKEY_LOCAL_MACHINE, 'SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\DSM2_v8_is1','UninstallString', Uninstallexepath2);

          /// show message box to uninstall previous v8
          /// StringChangeEx(Uninstallexepath2, '"',   '', True);

          /// if UninstallerNotCalled(Uninstallexepath1,Uninstallexepath2) then
          ///  begin
          ///    Abort;
          ///  end;

       end;


    Sleep(600);
			
		/// Read system environment path
    RegQueryStringValue(HKEY_LOCAL_MACHINE, 'SYSTEM\CurrentControlSet\Control\Session Manager\Environment', 'Path',       OldPath);

        /// Clean double ;
        StringChangeEx(OldPath,       ';;',       ';', True);
        /// Clean path written by previous version
        StringChangeEx(OldPath,       '%VISTA_HOME%\bin;',       '', True);
        StringChangeEx(OldPath,       ';%VISTA_HOME%\bin',       '', True);
        StringChangeEx(OldPath,       '%PTM_HOME%\bin;',         '', True);
        StringChangeEx(OldPath,       ';%PTM_HOME%\bin',         '', True);
        StringChangeEx(OldPath,       '%DSM2_HOME%\bin;',        '', True);
        StringChangeEx(OldPath,       ';%DSM2_HOME%\bin',        '', True);
        StringChangeEx(OldPath,       '%DSM2_HOME%\vista\bin;',  '', True);
        StringChangeEx(OldPath,       ';%DSM2_HOME%\vista\bin',  '', True);

        ///Prepend path
        OldPath        := '%DSM2_HOME%\vista\bin;'  + OldPath;
        OldPath        := '%DSM2_HOME%\bin;'        + OldPath;

        RegWriteExpandStringValue(HKEY_LOCAL_MACHINE, 'SYSTEM\CurrentControlSet\Control\Session Manager\Environment', 'Path',       OldPath);


        ///clean unused system python path
        RegQueryStringValue(HKEY_LOCAL_MACHINE, 'SYSTEM\CurrentControlSet\Control\Session Manager\Environment', 'PYTHONPATH', OldPythonPath);
        StringChangeEx(OldPythonPath, ';;',   ';', True);
        StringChangeEx(OldPythonPath, '%SCRIPTS_HOME%;',   '', True);
        StringChangeEx(OldPythonPath, ';%SCRIPTS_HOME%',   '', True);

        if OldPythonPath = '' then

          begin  ///delete value
            RegDeleteValue(HKEY_LOCAL_MACHINE, 'SYSTEM\CurrentControlSet\Control\Session Manager\Environment', 'PYTHONPATH');
          end
          
        else

          begin
            RegWriteExpandStringValue(HKEY_LOCAL_MACHINE, 'SYSTEM\CurrentControlSet\Control\Session Manager\Environment', 'PYTHONPATH', OldPythonPath);
          end;


        /// clean system environment written by previous version.
        RegDeleteValue(HKEY_LOCAL_MACHINE, 'SYSTEM\CurrentControlSet\Control\Session Manager\Environment', 'PTM_HOME');
        RegDeleteValue(HKEY_LOCAL_MACHINE, 'SYSTEM\CurrentControlSet\Control\Session Manager\Environment', 'SCRIPTS_HOME');



    /// clean user environment path written by previous version.
    RegQueryStringValue(HKEY_CURRENT_USER, 'Environment', 'Path',       OldPath);

        /// Clean double ;
        StringChangeEx(OldPath,       ';;',       ';', True);
        /// clean user environment path written by previous version.
        StringChangeEx(OldPath,       '%PTM_HOME%\bin;',         '', True);
        StringChangeEx(OldPath,       ';%PTM_HOME%\bin',         '', True);
        StringChangeEx(OldPath,       '%VISTA_HOME%\bin;',       '', True);
        StringChangeEx(OldPath,       ';%VISTA_HOME%\bin',       '', True);
        StringChangeEx(OldPath,       '%DSM2_HOME%\bin;',        '', True);
        StringChangeEx(OldPath,       ';%DSM2_HOME%\bin',        '', True);
        StringChangeEx(OldPath,       '%DSM2_HOME%\vista\bin;',  '', True);
        StringChangeEx(OldPath,       ';%DSM2_HOME%\vista\bin',  '', True);

        RegWriteExpandStringValue(HKEY_CURRENT_USER, 'Environment', 'Path',       OldPath);

        ///clean unused python path
        RegQueryStringValue(HKEY_CURRENT_USER, 'Environment', 'PYTHONPATH', OldPythonPath);
        StringChangeEx(OldPythonPath, ';;',   ';', True);
        StringChangeEx(OldPythonPath, '%SCRIPTS_HOME%;',   '', True);
        StringChangeEx(OldPythonPath, ';%SCRIPTS_HOME%',   '', True);

        if OldPythonPath = '' then

          begin
            RegDeleteValue(HKEY_CURRENT_USER, 'Environment', 'PYTHONPATH');
          end
          
        else

          begin
            RegWriteExpandStringValue(HKEY_CURRENT_USER, 'Environment', 'PYTHONPATH', OldPythonPath);
          end;

        /// clean user environment written by previous version.
        RegDeleteValue(HKEY_CURRENT_USER, 'Environment', 'DSM2_HOME');
        RegDeleteValue(HKEY_CURRENT_USER, 'Environment', 'PTM_HOME');
        RegDeleteValue(HKEY_CURRENT_USER, 'Environment', 'VISTA_HOME');
        RegDeleteValue(HKEY_CURRENT_USER, 'Environment', 'SCRIPTS_HOME');

 end;
 end;
 end;




